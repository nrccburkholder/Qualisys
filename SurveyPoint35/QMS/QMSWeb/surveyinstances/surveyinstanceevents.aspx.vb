Partial Class frmSurveyInstanceEvents
    Inherits System.Web.UI.Page

#Region " Web Form Designer Generated Code "

    'This call is required by the Web Form Designer.
    <System.Diagnostics.DebuggerStepThrough()> Private Sub InitializeComponent()

    End Sub

    Private Sub Page_Init(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles MyBase.Init
        'CODEGEN: This method call is required by the Web Form Designer
        'Do not modify it using the code editor.
        InitializeComponent()
    End Sub

#End Region

    Private m_oSurveyInstanceEvents As clsSurveyInstanceEvent

    Private Const DATASET_NAME As String = "SURVEYINSTANCES/SURVEYINSTANCEEVENTS"

    Private Sub Page_Load(ByVal sender As System.Object, ByVal e As System.EventArgs) Handles MyBase.Load
        m_oSurveyInstanceEvents = New clsSurveyInstanceEvent(Application("DSN"))

        'Determine whether page setup is required:
        'user has posted back to same page
        If Page.IsPostBack Then
            'user has dataset 
            If Not IsNothing(CType(Session("ds"), DataSet)) Then
                'dataset generated by page
                If m_oSurveyInstanceEvents.VerifyNamedDataSet(CType(Session("ds"), DataSet), DATASET_NAME) Then
                    'no further setup required
                    Exit Sub

                End If

            End If

        End If

        'security check
        If QMS.clsQMSSecurity.CheckPrivledge(CType(Session("Privledges"), ArrayList), QMS.qmsSecurity.INSTANCE_VIEWER) Then
            Viewstate("LastSort") = ""

            'format datagrid
            QMS.clsQMSTools.FormatQMSDataGrid(dgSurveyInstanceEvents)

            m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.SurveyInstanceID) = _
                Integer.Parse("0" & Request.QueryString("siid"))
            m_oSurveyInstanceEvents.GetDetails()

            LoadSurveyInstanceEventsGrid("EventID")
            UpdateSurveyInstanceLink(m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.SurveyInstanceID))
            SecuritySetup()

        Else
            'user does not have survey instance viewing privledges, reflect
            Response.Redirect(QMS.clsQMSSecurity.NonPrivAccess(Request, Session))

        End If

    End Sub

    Private Sub UpdateSurveyInstanceLink(ByVal iSurveyInstanceID As Integer)
        Dim si As New QMS.clsSurveyInstances(DMI.DataHandler.sConnection)
        si.FillAll(iSurveyInstanceID)
        hlSurveyInstance.Text = String.Format("{0}:&nbsp;{1}:&nbsp;{2}", _
            si.MainDataTable.Rows(0).Item("SurveyName"), _
            si.MainDataTable.Rows(0).Item("ClientName"), _
            si.MainDataTable.Rows(0).Item("Name"))
        hlSurveyInstance.NavigateUrl = String.Format("surveyinstancedetails.aspx?id={0}", iSurveyInstanceID)

        si = Nothing

    End Sub

    Private Sub LoadSurveyInstanceEventsGrid(Optional ByVal sSortBy As String = "")

        If sSortBy.Length > 0 Then Me.m_oSurveyInstanceEvents.DataSet.Tables("SurveyInstanceEvents").DefaultView().Sort = sSortBy
        Session("ds") = Me.m_oSurveyInstanceEvents.NamedDataSet(DATASET_NAME)

        dgSurveyInstanceEvents.DataSource = Me.m_oSurveyInstanceEvents.DataSet.Tables("SurveyInstanceEvents")
        dgSurveyInstanceEvents.DataKeyField = "SurveyInstanceEventID"
        dgSurveyInstanceEvents.DataBind()

    End Sub

    Private Sub SecuritySetup()
        If Not QMS.clsQMSSecurity.CheckPrivledge(CType(Session("Privledges"), ArrayList), QMS.qmsSecurity.INSTANCE_ADMIN) Then
            'must be instance administrator to edit instance events
            dgSurveyInstanceEvents.Columns(0).Visible = False

        End If

    End Sub

    Private Sub dgSurveyInstanceEvents_CancelCommand(ByVal source As Object, ByVal e As System.Web.UI.WebControls.DataGridCommandEventArgs) Handles dgSurveyInstanceEvents.CancelCommand
        dgSurveyInstanceEvents.EditItemIndex = -1
        LoadSurveyInstanceEventsGrid()

    End Sub

    Private Sub dgSurveyInstanceEvents_EditCommand(ByVal source As Object, ByVal e As System.Web.UI.WebControls.DataGridCommandEventArgs) Handles dgSurveyInstanceEvents.EditCommand
        If dgSurveyInstanceEvents.EditItemIndex = -1 Then
            dgSurveyInstanceEvents.EditItemIndex = e.Item.ItemIndex
            LoadSurveyInstanceEventsGrid()

        End If

    End Sub

    Private Sub dgSurveyInstanceEvents_PageIndexChanged(ByVal source As Object, ByVal e As System.Web.UI.WebControls.DataGridPageChangedEventArgs) Handles dgSurveyInstanceEvents.PageIndexChanged
        If dgSurveyInstanceEvents.EditItemIndex = -1 Then
            dgSurveyInstanceEvents.CurrentPageIndex = e.NewPageIndex
            dgSurveyInstanceEvents.EditItemIndex = -1
            LoadSurveyInstanceEventsGrid()

        End If

    End Sub

    Private Sub dgSurveyInstanceEvents_SortCommand(ByVal source As Object, ByVal e As System.Web.UI.WebControls.DataGridSortCommandEventArgs) Handles dgSurveyInstanceEvents.SortCommand
        Dim sLastSort As String
        Dim sSortBy As String

        If dgSurveyInstanceEvents.EditItemIndex = -1 Then
            sLastSort = Viewstate("LastSort").ToString

            If sLastSort = e.SortExpression Then
                sSortBy = e.SortExpression & " ASC"
                Viewstate("LastSort") = ""

            Else
                sSortBy = e.SortExpression & " DESC"
                Viewstate("LastSort") = e.SortExpression

            End If

            LoadSurveyInstanceEventsGrid(sSortBy)

        End If

    End Sub

    Private Sub dgSurveyInstanceEvents_UpdateCommand(ByVal source As Object, ByVal e As System.Web.UI.WebControls.DataGridCommandEventArgs) Handles dgSurveyInstanceEvents.UpdateCommand
        Dim iSurveyInstanceID As Integer
        Dim iSurveyInstanceEventID As Integer
        Dim bUseEvent As Boolean

        If Page.IsValid Then
            iSurveyInstanceID = m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.SurveyInstanceID)
            iSurveyInstanceEventID = dgSurveyInstanceEvents.DataKeys(e.Item.ItemIndex)
            bUseEvent = CType(e.Item.FindControl("cbUseEvent"), CheckBox).Checked

            If bUseEvent Then
                With e.Item
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.SurveyInstanceEventID) = _
                        iSurveyInstanceEventID
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.SurveyInstanceID) = _
                        iSurveyInstanceID
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.EventID) = _
                        Integer.Parse(.Cells(1).Text)
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.TranslationValue) = _
                        CType(.FindControl("tbTranslationValue"), TextBox).Text
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.NonContactHours) = _
                        Integer.Parse(CType(.FindControl("tbNonContactHours"), TextBox).Text)
                    m_oSurveyInstanceEvents.Details(tblSurveyInstanceEvents.Final) = _
                        CType(.FindControl("cbFinal"), CheckBox).Checked
                    m_oSurveyInstanceEvents.Submit()

                End With

                If iSurveyInstanceEventID > 0 Then
                    DMI.WebFormTools.Msgbox(Me, "Survey Instance Event Updated")

                Else
                    DMI.WebFormTools.Msgbox(Me, "Survey Instance Event Added")

                End If

            ElseIf iSurveyInstanceEventID > 0 Then
                m_oSurveyInstanceEvents.Delete(iSurveyInstanceEventID)
                DMI.WebFormTools.Msgbox(Me, "Survey Instance Event Removed")

            End If

            With m_oSurveyInstanceEvents
                .Clear()
                .Details(tblSurveyInstanceEvents.SurveyInstanceID) = _
                    iSurveyInstanceID
                .GetDetails()
            End With

            dgSurveyInstanceEvents.EditItemIndex = -1

            LoadSurveyInstanceEventsGrid()

        End If

    End Sub

End Class
